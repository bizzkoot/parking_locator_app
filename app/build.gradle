plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.github.breadmoirai.github-release' version '2.2.12'
}

import groovy.json.JsonSlurper

def getNextVersion() {
    def apiUrl = new URL("https://api.github.com/repos/bizzkoot/parking_locator_app/releases")
    def connection = apiUrl.openConnection()
    connection.requestMethod = 'GET'
    connection.setRequestProperty("User-Agent", "parking_locator_app")

    try {
        def response = new JsonSlurper().parse(connection.inputStream)
        def latestRelease = response[0]
        def currentVersion = latestRelease.tag_name.replace('v', '')

        def parts = currentVersion.split('\\.')
        def major = parts[0].toInteger()
        def minor = parts[1].toInteger()
        def patch = parts[2].toInteger()

        if (patch == 99) {
            patch = 0
            minor++
        } else {
            patch++
        }

        return "${major}.${minor}.${patch}"
    } catch (Exception e) {
        println "Error retrieving version information: ${e.message}"
        return "1.0.0" // default version
    }
}

android {
    namespace 'com.bizzkoot.parkinglocator'
    compileSdk 35

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "com.bizzkoot.parkinglocator"
        minSdk 24
        targetSdk 34
        def nextVersion = getNextVersion()
        versionName nextVersion
        versionCode nextVersion.split('\\.').inject(0) { 
            code, component -> code * 100 + component.toInteger() 
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            def keystorePropertiesFile = rootProject.file("keystore/keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                storeFile file("${rootDir}/keystore/release.keystore")
                storePassword System.getenv('KEYSTORE_PASSWORD')
                keyAlias "parking_locator"
                keyPassword System.getenv('KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "Parking Locator v${variant.versionName}.apk"
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
}
dependencies {
    implementation 'androidx.core:core-splashscreen:1.0.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.transition:transition:1.5.1'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.camera:camera-camera2:1.4.0'
    implementation 'androidx.camera:camera-lifecycle:1.4.0'
    implementation 'androidx.camera:camera-view:1.4.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation "androidx.exifinterface:exifinterface:1.3.7"
    implementation 'androidx.compose.material3:material3:1.3.1'
}